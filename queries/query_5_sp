PREFIX nellonto: <https://w3id.org/nellrdf/sp/ontology/>
PREFIX meta: <https://w3id.org/nellrdf/sp/metadata/>

#query_5_sp

SELECT DISTINCT ?country ?label (COUNT(*) as ?count) WHERE  {
    ?country rdfs:label ?label.

    ?city ?sing_city_loc_in_country ?country.
    ?sing_city_loc_in_country rdf:singletonPropertyOf nellonto:citylocatedincountry.
    
    ?mayor ?sing_type_female nellonto:female.
    ?sing_type_female rdf:singletonPropertyOf rdf:type.

    {
        ?mayor ?sing_lead1 ?city.
        ?sing_lead1 rdf:singletonPropertyOf nellonto:personleadscity.
    } UNION {
        ?mayor ?sing_lead2 ?city.
        ?sing_lead2 rdf:singletonPropertyOf nellonto:personleadsgeopoliticalorganization.

        ?city ?sing_type_city nellonto:city.
        ?sing_type_city rdf:singletonPropertyOf rdf:type.
    }
}
GROUP BY ?country ?label
ORDER BY ?count
